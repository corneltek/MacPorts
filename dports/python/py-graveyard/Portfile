# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem  1.0

name        py-graveyard
version     0.0
categories  python

array set replacements {
    24 27
    25 27
    26 27
    31 34
    32 34
}

proc unknown args {
    upvar 1 replacements replacements
    foreach series [lassign $args port vers] {
        lassign [split $vers _] ver rev
        if {$rev eq ""} {
            set rev 0
        }
        set repl $replacements($series)
        subport py${series}-$port {
            version $ver
            revision $rev
            replaced_by py${repl}-$port
        }
    }
}

abjad               1.1.1_1     25
antlr3              3.1.3_1     25
aspects             1.3_1       25
blessings           1.5_1       25 31 32
boto                2.8.0_1     25
buzhug              1.8_1       25
cherrypy3           3.2.0_1     24 25
creoleparser        0.7.4_1     25
dap                 2.2.6.7_1   24 25
nose-progressive    1.3_1       25

if {${subport} ne ${name}} {
    PortGroup obsolete 1.0
} else {
    maintainers         larryv openmaintainer
    supported_archs     noarch
    universal_variant   no
    description         Metaport containing obsolete Python subports
    long_description    This is a metaport for collecting obsolete \
                        Python subports so they don't cause spurious \
                        build failures on the MacPorts buildslaves \
                        whenever their superports are modified. It \
                        cannot be installed.
    pre-fetch {
        ui_error {This is a metaport for obsolete Python subports.}
        return -code error {non-installable metaport}
    }
}
