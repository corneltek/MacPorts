# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           github 1.0

name                raxml
github.setup        stamatak standard-RAxML 7.7.6 v
checksums           sha256  7562b307bc50e38162f104271793a1c8f98f866c2342bea21afe6df6a982fd82 \
                    rmd160  da76d6623515b7d8f78a69aaf4eec713508789af
name                raxml
description         Estimation of phylogenetic trees
long_description    RAxML is a program for sequential and parallel \
                    estimation of phylogenetic trees written by \
                    Alexandros Stamatakis.
license             GPL-2+
maintainers         gmail.com:mattoates
categories          science biology
homepage            http://www.exelixis-lab.org/
platforms           darwin

use_configure       no

#Strings used by the variant options
set HPCm ""
set HPCb ""
set EXTm ""
set EXTb ""

#Use the HYBRID implementation

variant hybrid conflicts pthreads mpi description {Hybrid MPI implementation} {
    depends_lib-append  port:openmpi
    set HPCm ".HYBRID"
    set HPCb "-HYBRID"
    configure.cc        ${prefix}/bin/openmpicc
}

#Use the MPI implementation
variant mpi conflicts pthreads hybrid description {MPI implementation} {
    depends_lib-append  port:openmpi
    set HPCm ".MPI"
    set HPCb "-MPI"
    configure.cc        ${prefix}/bin/openmpicc
}

#Use the pthreads implementation (default)
variant pthreads conflicts mpi hybrid description {Pthreads implementation} {
    set HPCm ".PTHREADS"
    set HPCb "-PTHREADS"
}

if {![variant_isset mpi]} {
    default_variants    +pthreads
}

#For Intel machines add SSE and AVX as a variant and use it by default with pthreads
if {${build_arch} == "i386" || ${build_arch} == "x86_64"} {
    variant sse3 conflicts avx description {Use the SSE3 extended instruction set from Intel since 2004} {
        set EXTm ".SSE3"
        set EXTb "-SSE3"
    }

    variant avx conflicts sse3 description {Use the AVX extended instruction set from Intel i7 (sandy bridge) and AMD Bulldozer} {
        set EXTm ".AVX"
        set EXTb "-AVX"
    }

    if {![catch {sysctl hw.optional.sse3} result] && ${result} == 1} {
        if {![catch {sysctl hw.optional.avx1_0} result] && ${result} == 1} {
            default_variants-append +avx
        } else {
            default_variants-append  +sse3
        }
    }
} else {
    post-patch {
        reinplace "s/-msse//" ${worksrcpath}/Makefile.gcc \
                              ${worksrcpath}/Makefile.PTHREADS.gcc \
                              ${worksrcpath}/Makefile.MPI.gcc
    }
}

#Pass in architecture information
build.args              EXTRA_CFLAGS="${configure.cc_archflags}" \
                        LDFLAGS="${configure.ld_archflags}"

#Make sure we are using the correct Makefile for this variant combo
pre-build {
    build.pre_args      -f Makefile${EXTm}${HPCm}.gcc
    build.args-append   CC="${configure.cc}"
}

#We only need the single raxml binary
destroot {
    copy ${worksrcpath}/raxmlHPC${HPCb}${EXTb} ${destroot}${prefix}/bin/raxml
}
