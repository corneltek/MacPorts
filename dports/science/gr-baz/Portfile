# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           cmake 1.0
PortGroup           github 1.0
PortGroup           active_variants 1.1

name                gr-baz

if {"${subport}" eq "${name}"} {

    github.setup        balint256 gr-baz c84b9d6c798ae1bf01beaa2045b241e426edc13b
    version             20140330
    checksums           rmd160 4eeafae773edecff5b87c3333f560e0e2fa00f5d \
                        sha256 06140275664dc73e37af484ea5f52914a7974d67de54c5a1f69694a2f2e6b9eb

    # allow gr-baz to work with both gnuradio and gnuradio-devel ...

    depends_lib-append  path:lib/libgnuradio-runtime.dylib:gnuradio

    # ... but not with gnuradio-legacy or gnuradio-next

    pre-fetch {
        if {![catch {set installed [lindex [registry_active gnuradio-legacy] 0]}]} {
            # gnuradio-legacy is installed; this version of gr-osmosdr does not work with gnuradio-legacy
            ui_msg "\nError: ${name} requires the gnuradio or gnuradio-devel port, and will not work with the gnuradio-legacy port.  deactivate gnuradio-legacy, and then install or activate gnuradio or gnuradio-devel.\n"
            return -code error "Invalid port dependency: gnuradio-legacy"
        }
        if {![catch {set installed [lindex [registry_active gnuradio-next] 0]}]} {
            # gnuradio-next is installed; this version of gr-osmosdr does not work with gnuradio-next
            ui_msg "\nError: ${name} requires the gnuradio or gnuradio-devel port, and will not work with the gnuradio-next port.  deactivate gnuradio-next, and then install or activate gnuradio or gnuradio-devel.\n"
            return -code error "Invalid port dependency: gnuradio-next"
        }
    }
}

subport gr-baz-legacy {

    github.setup        balint256 gr-baz dd9540661065308eb9529b81d1580b861dd8aa0e
    version             20130329
    checksums           rmd160 06b6152d541169116219d701970fc03710e0fbba \
                        sha256 377a0058ec72ed963edbc65ef062180d6937d4fda334bc2e7469146f0abfd626

    # fix name from github setup

    name                gr-baz-legacy

    # requires gnuradio-legacy

    depends_lib-append  port:gnuradio-legacy
}

homepage            http://wiki.spench.net/wiki/Gr-baz

categories          science comms
maintainers         michaelld openmaintainer
description         Provides augmented functionality (blocks, GRC definitions, apps, etc) for GNU Radio.
long_description    ${description}
license             GPL-3
platforms           darwin

depends_build-append port:pkgconfig
depends_lib-append	port:boost

# do VPATH build

post-extract        { file mkdir ${workpath}/build }
configure.dir       ${workpath}/build
build.dir           ${workpath}/build

# remove top-level include path, such that internal headers are used
# instead of any already-installed ones.

configure.cppflags-delete -I${prefix}/include

# remove top-level library path, such that internal libraries are used
# instead of any already-installed ones.

configure.ldflags-delete -L${prefix}/lib

# set last configure argument to the reletive path
# to the top-level cmake source

configure.post_args ../${worksrcdir}

# be verbose when building, for debugging purposes

build.post_args VERBOSE=1

# set Python variants

set pythons_suffixes {26 27}
global chosen_python_suffix
set chosen_python_suffix ""

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [join [split ${s} ""] "."]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    eval [subst {
        variant ${p} description "Build ${name} for Python ${v}" \
            conflicts ${c} {

            set chosen_python_suffix ${s}

            if {"${subport}" eq "${name}"} {
                # require gnuradio to also have this Python variant
                require_active_variants \
                    path:lib/libgnuradio-runtime.dylib:gnuradio ${p}
            } else {
                # require gnuradio-legacy to also have this Python variant
                require_active_variants port:gnuradio-legacy ${p}
            }

            depends_lib-append \
                port:${p}

            # specify that Python version to use
            configure.args-append \
                -DPYTHON_EXECUTABLE=${prefix}/bin/python${v} \
                -DPYTHON_INCLUDE_DIR=${prefix}/Library/Frameworks/Python.framework/Versions/${v}/Headers \
                -DPYTHON_LIBRARY=${prefix}/Library/Frameworks/Python.framework/Versions/${v}/Python \
                -DGR_PYTHON_DIR=${frameworks_dir}/Python.framework/Versions/${v}/lib/python${v}/site-packages
        }
    }]
}

# if no python variant is set, default to 2.7
if {![variant_isset python26] &&
    ![variant_isset python27]} {
    default_variants +python27
}

# make sure -python27 was not used alone
if {![variant_isset python26] &&
    ![variant_isset python27]} {
    return -code error "Exactly one Python variant must be selected."
}
