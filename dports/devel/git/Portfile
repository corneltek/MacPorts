# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0

name                git
version             2.1.3
description         A fast version control system
long_description    Git is a fast, scalable, distributed open source version \
                    control system focusing on speed and efficiency.
maintainers         ciserlohn
categories          devel
license             GPL-2 LGPL-2.1+
platforms           darwin
homepage            http://git-scm.com/
master_sites        https://www.kernel.org/pub/software/scm/git/
distname            git-${version}
distfiles           git-${version}${extract.suffix} \
                    git-manpages-${version}${extract.suffix}

checksums           git-${version}${extract.suffix} \
                    rmd160  d7745715786b1e78133f379b0708e33207749d80 \
                    sha256  525721c225b406ae05d31a0fb0fa4ed3f5a81e17ff0b72424e39093b9227c089 \
                    git-manpages-${version}${extract.suffix} \
                    rmd160  a97bac0f95945228de57bf7e66c40390821f2c58 \
                    sha256  0f5ad9353282bf34599a05d0d07371bdd23f840d31dcf222fa5836f06518c0b1 \

depends_run         port:rsync

depends_lib         port:curl \
                    port:zlib \
                    port:openssl \
                    port:expat \
                    port:libiconv

if {[variant_isset perl5_12]} {
    set perl_version 5.12
} elseif {[variant_isset perl5_14]} {
    set perl_version 5.14
} else {
    set perl_version 5.16
    default_variants +perl5_16
}


depends_run-append  port:p${perl_version}-authen-sasl \
                    port:p${perl_version}-error \
                    port:p${perl_version}-net-smtp-ssl \
                    port:p${perl_version}-term-readkey

depends_lib-append  port:perl${perl_version}

patchfiles          patch-Makefile.diff git-subtree.1.diff
patch.pre_args      -p1

extract.only        git-${version}${extract.suffix} \
                    git-manpages-${version}${extract.suffix}

use_configure       no

variant universal   {}

set CFLAGS          "-I. -Wall -O2 -I${prefix}/include [get_canonical_archflags cc]"
set LDFLAGS         "-L${prefix}/lib [get_canonical_archflags ld]"

build.args          CFLAGS="${CFLAGS}" \
                    LDFLAGS="${LDFLAGS}" \
                    CC=${configure.cc} \
                    prefix=${prefix} \
                    CURLDIR=${prefix} \
                    OPENSSLDIR=${prefix} \
                    ICONVDIR=${prefix} \
                    PERL_PATH="${prefix}/bin/perl${perl_version}" \
                    NO_FINK=1 \
                    NO_DARWIN_PORTS=1 \
                    NO_R_TO_GCC_LINKER=1 \
                    V=1

test.run            yes
test.cmd            make
test.target         test
test.dir            ${worksrcpath}
pre-test {
    eval test.args  ${build.args}
}

destroot.target     install
pre-destroot {
    eval destroot.args  ${build.args}
    xinstall -m 644 ${worksrcpath}/contrib/subtree/git-subtree.1 ${workpath}/man1
}

set docdestroot ${destroot}${prefix}/share/doc/git-doc

post-destroot {
    foreach f {1 5 7} {
        xinstall -d ${destroot}${prefix}/share/man/man${f}
        foreach m [glob -directory ${workpath} man${f}/*.${f}] {
            xinstall ${m} ${destroot}${prefix}/share/man/man${f}
        }
    }
    if {![variant_isset svn]} {
        system "rm ${destroot}${prefix}/libexec/git-core/git-svn*"
    }
    xinstall -d ${docdestroot}

    fs-traverse badfile ${destroot} {
        if {[string last perllocal.pod ${badfile}] != -1} {
            ui_info "Removing ${badfile}"
            file delete ${badfile}
        }
    }

    set completions_path ${destroot}${prefix}/share/bash-completion/completions
    xinstall -d ${completions_path}
    xinstall -m 644 ${worksrcpath}/contrib/completion/git-completion.bash \
        ${completions_path}/git
   
    set share_path ${destroot}${prefix}/share/${name}
    xinstall -d ${share_path}
    xinstall -m 644 ${worksrcpath}/contrib/completion/git-prompt.sh \
        ${share_path}/git-prompt.sh

    xinstall -m 755 ${worksrcpath}/contrib/subtree/git-subtree.sh \
        ${destroot}${prefix}/libexec/git-core/git-subtree

    file delete -force ${share_path}/contrib
    copy ${worksrcpath}/contrib ${share_path}

    xinstall -m 755 -d ${destroot}${prefix}/share/emacs/site-lisp/
    eval xinstall -m 644 [glob ${worksrcpath}/contrib/emacs/*.el] \
        ${destroot}${prefix}/share/emacs/site-lisp/

}

if {![variant_isset python26] && ![variant_isset python27]} {
    default_variants    +python27
}

variant python26 conflicts python27 description {Use Python 2.6} {
    build.args-append       PYTHON_PATH="${prefix}/bin/python2.6"
    depends_lib-append      port:python26
}

variant python27 conflicts python26 description {Use Python 2.7} {
    build.args-append       PYTHON_PATH="${prefix}/bin/python2.7"
    depends_lib-append      port:python27
}

variant perl5_16 conflicts perl5_14 perl5_12 description {use perl 5.16} {}
variant perl5_14 conflicts perl5_16 perl5_12 description {use perl 5.14} {}
variant perl5_12 conflicts perl5_16 perl5_14 description {use perl 5.12} {}

variant pcre {
    build.args-append       LIBPCREDIR=${prefix} USE_LIBPCRE=1
    depends_lib-append      port:pcre
}

variant doc description {Install HTML and plaintext documentation} {
    distfiles-append        git-htmldocs-${version}${extract.suffix}
    checksums-append        git-htmldocs-${version}${extract.suffix} \
                            rmd160  e726fc9ff9d0fb91129ce05878e0a830475616a3 \
                            sha256  36978b647dfb8256622dd33e7d9a493362078f6ec5ec65c5a1dffd3c6ac29474

    patchfiles-append       git-subtree.html.diff

    post-extract {
        file mkdir ${workpath}/htmldocs
        system -W ${workpath}/htmldocs "${extract.cmd} ${extract.pre_args} \
            ${distpath}/git-htmldocs-${version}${extract.suffix} \
            ${extract.post_args}"
    }

    pre-destroot {
        xinstall -m 644 ${worksrcpath}/contrib/subtree/git-subtree.html ${workpath}/htmldocs
    }

    post-destroot {
        foreach f [glob ${workpath}/htmldocs/*] {
            file delete -force "${docdestroot}/[file tail ${f}]"
            file attribute ${f} -permissions ugo+r
            copy ${f} ${docdestroot}
        }
    }
}

variant gitweb description {Install gitweb.cgi} {
    build.target-append     gitweb/gitweb.cgi

    post-destroot {
        xinstall -d ${destroot}${prefix}/share/${name}/gitweb
        xinstall -W ${worksrcpath}/gitweb \
            gitweb.cgi \
            ${destroot}${prefix}/share/${name}/gitweb
        xinstall -m 444 -W ${worksrcpath}/gitweb/static \
            gitweb.css \
            gitweb.js \
            git-favicon.png \
            git-logo.png \
            ${destroot}${prefix}/share/${name}/gitweb
        xinstall -d ${docdestroot}/gitweb
        xinstall -m 444 -W ${worksrcpath}/gitweb README INSTALL \
            ${docdestroot}/gitweb
    }
}

variant svn description {Bi-directional subversion repository support} {
    depends_run-append  port:subversion \
                        port:p${perl_version}-libwww-perl \
                        port:p${perl_version}-svn-simple
}

variant bash_completion {
    depends_run-append  path:etc/bash_completion:bash-completion
}


variant credential_osxkeychain description {Install git credential-osxkeychain utility from contrib} {

    post-build {
        system -W "${worksrcpath}/contrib/credential/osxkeychain" "make [join ${build.args}]"
    }

    pre-destroot {
        xinstall -m 755 "${worksrcpath}/contrib/credential/osxkeychain/git-credential-osxkeychain" \
            "${destroot}${prefix}/bin/"
    }
}

platform darwin 8 {
    build.args-append   NO_APPLE_COMMON_CRYPTO=1
}

default_variants    +doc +pcre +credential_osxkeychain

livecheck.type          regexm
livecheck.regex         {<span class="version">.*?(\d\.\d\.\d).*?</span>}
