# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0

name                mysql51
set name_mysql      ${name}
version             5.1.63
# Please set revision_client and revision_server to 0 if you bump version.
set revision_client 4
set revision_server 3
categories          databases
platforms           darwin
license             GPL-2
maintainers         pixilla openmaintainer

if {"darwin" == ${os.platform} && ${os.major} > 8} {
    set mysqluser       _mysql
} else {
    set mysqluser       mysql
}

if {$subport == $name} {

    # http://bugs.mysql.com/bug.php?id=47360
    PortGroup           muniversal 1.0
    PortGroup           archcheck 1.0
    PortGroup           select 1.0
    
    set branch          [join [lrange [split ${version} .] 0 1] .]

    revision            ${revision_client}
    license             GPL-2
    description         Multithreaded SQL database server
    long_description    MySQL is an open-source, multi-threaded SQL database with a command \
                        syntax very similar to mSQL.
    
    homepage            http://www.mysql.com/
    master_sites \
        http://mysql.mirrors.pair.com/Downloads/MySQL-${branch}/ \
        http://mysql.he.net/Downloads/MySQL-${branch}/ \
        http://mirrors.sunsite.dk/mysql/Downloads/MySQL-${branch}/ \
        http://sunsite.informatik.rwth-aachen.de/mysql/Downloads/MySQL-${branch}/ \
        http://mirror.facebook.net/mysql/Downloads/MySQL-${branch}/ \
        http://ftp.plusline.de/mysql/Downloads/MySQL-${branch}/
    
    
    distname            mysql-${version}
    use_parallel_build  yes
    
    patch.pre_args      -p1
    patchfiles          patch-Makefile.in.diff \
                        patch-scripts-mysql_secure_installation.sh.diff

    checksums           md5     672167c3f03f969febae66c43859d76d \
                        rmd160  d5e9afab1d7fdce959400376398593b59fff6814 \
                        sha256  29461a3792e224d27d98c5bd7d7f761be196d6c215e73b4440e11505489f1857
    
    depends_lib         port:zlib
    
    depends_run         port:mysql_select
    
    select.group        mysql
    select.file         ${filespath}/${name_mysql}
    
    archcheck.files     lib/libz.dylib
    
    post-extract {
        file mkdir ${worksrcpath}/build_macports/macports
        copy ${filespath}/macports-default.cnf \
            ${filespath}/my.cnf \
            ${worksrcpath}/build_macports/macports/
    }

    post-patch {
        reinplace "s%@BINDIR@%${prefix}/lib/${name_mysql}/bin%g" \
            ${worksrcpath}/scripts/mysql_secure_installation.sh
        reinplace "s|@NAME@|${name_mysql}|g" \
            ${worksrcpath}/build_macports/macports/macports-default.cnf \
            ${worksrcpath}/build_macports/macports/my.cnf
        reinplace "s|@PREFIX@|${prefix}|g" \
            ${worksrcpath}/build_macports/macports/macports-default.cnf \
            ${worksrcpath}/build_macports/macports/my.cnf
    }
    
    configure.args \
        --bindir=${prefix}/lib/${name_mysql}/bin \
        --sbindir=${prefix}/lib/${name_mysql}/bin \
        --libexecdir=${prefix}/lib/${name_mysql}/bin \
        --sysconfdir=${prefix}/etc/${name_mysql} \
        --localstatedir=${prefix}/var/db/${name_mysql} \
        --libdir=${prefix}/lib/${name_mysql} \
        --includedir=${prefix}/include/${name_mysql} \
        --datarootdir=${prefix}/share/${name_mysql} \
        --infodir=${prefix}/share/info/${name_mysql} \
        --mandir=${prefix}/share/man/${name_mysql} \
        --with-zlib-dir=${prefix} \
        --without-ssl \
        --with-extra-charsets=complex \
        --with-unix-socket-path=${prefix}/var/run/${name_mysql}/mysqld.sock \
        --with-mysqld-user=${mysqluser} \
        --without-docs \
        --with-plugins=all \
        --enable-thread-safe-client
    
    # Build libmysqld embedded server.
    configure.cflags-append -fPIC
    configure.cxxflags-append -fPIC
    configure.args-append --with-embedded-server --with-pic
    
    # BUILD FIX TODO: clang, build system
    # doesn't build with clang as of 2011.06.27
    # This looks like a bug in mysql's build system
    if {${configure.compiler} == "clang"} {
        configure.compiler llvm-gcc-4.2
    }
    
    post-build {
        if {[variant_isset universal]} {
            set dirs {}
            foreach arch ${universal_archs_to_use} {
                lappend dirs ${worksrcpath}-${arch}
            }
        } else {
            set dirs ${worksrcpath}
        }
        foreach dir ${dirs} {
            reinplace -E {s|-arch [a-z0-9_]+||g} \
                ${dir}/scripts/mysql_config \
                ${dir}/scripts/mysqlbug
        }
    }
    
    post-destroot {
        # proc portdestroot::destroot_finish fails to find and compress our man pages
        # so borrow the compress command and run on our files now.
        set manpath "${destroot}${prefix}/share/man"
        set gzip [findBinary gzip ${portutil::autoconf::gzip_path}]
        foreach manpage [glob -type f ${destroot}${prefix}/share/man/${name_mysql}/man\[1-9\]/*] {
            # Fix paths in manpages
            reinplace "s|/etc/|${prefix}/etc/${name_mysql}/|g" ${manpage}
            # Compress all manpages with gzip
            system "$gzip -9vf ${manpage}"
        }
        foreach samp_conffile [glob -type f ${destroot}${prefix}/share/${name_mysql}/mysql/my-*.cnf] {
            # Fix paths in sample configuration files
            reinplace "s|/etc/my.cnf|${prefix}/etc/${name_mysql}/my.cnf|g" ${samp_conffile}
        }
        xinstall -m 755 -o root -d \
            ${destroot}${prefix}/etc/${name_mysql}
        copy ${worksrcpath}/build_macports/macports/macports-default.cnf \
            ${destroot}${prefix}/etc/${name_mysql}/
        xinstall -m 755 -o root -d \
            ${destroot}${prefix}/share/${name_mysql}/support-files/macports
        copy ${worksrcpath}/build_macports/macports/my.cnf \
            ${destroot}${prefix}/share/${name_mysql}/support-files/macports/
    }
    
    post-install {
        if {![file exists ${prefix}/etc/LaunchDaemons/org.macports.${name_mysql}/org.macports.${name_mysql}-server.plist]} {
            ui_msg "The ${name_mysql} client has been installed."
            ui_msg "To install the ${name_mysql} server, install the ${name_mysql}-server port."
        }
    }

    pre-activate {
        if { [file exists ${prefix}/etc/${name_mysql}/macports-default.cnf]
            && ![catch {set vers [lindex [registry_active ${name_mysql}-server] 0]}]
            && [vercmp [lindex $vers 1] 5.1.63] < 0 } {
        
            set deactivate_proc registry_deactivate_composite
            if {[info commands registry_deactivate_composite] == {}} {
                set deactivate_proc registry_deactivate
            }
            $deactivate_proc ${name_mysql}-server "" [list ports_nodepcheck 1]
        }
    }

    post-activate {
        if {![file exists ${prefix}/etc/${name_mysql}/my.cnf]} {
            copy ${prefix}/share/${name_mysql}/support-files/macports/my.cnf \
                ${prefix}/etc/${name_mysql}/
        }
    }
    
    variant openssl description {Enable OpenSSL support} {
    
        depends_lib-append      port:openssl
        configure.args-delete   --without-ssl
        configure.args-append   --with-ssl=${prefix}
        archcheck.files-append  lib/libssl.dylib
    }

    variant system_readline description {Use system readline instead of bundled readline} {
    
        # Add readline support.
        # "--without-readline" has the peculiar meaning "do not use the bundled copy
        # of readline but use the system's (i.e. MacPorts') copy of readline"
        depends_lib-append      port:readline
        configure.args-append   --without-readline
        archcheck.files-append  lib/libreadline.dylib
    }

    livecheck.type      regex
    livecheck.version   [lindex [split ${version} -] 0]
    livecheck.url       http://dev.mysql.com/
    livecheck.regex     "<a href=\"http://dev.mysql.com/downloads/mysql/\[^\"\]+\">(${branch}(\.\[0-9.\]+)?)\[^<\]*</a>"
}

subport ${name_mysql}-server {

    revision            ${revision_server}
    license             BSD
    description         Run ${name_mysql} as server
    long_description    ${description}
    
    supported_archs     noarch
    distfiles
    
    depends_run         port:${name_mysql}
    
    add_users ${mysqluser} group=${mysqluser} realname=MySQL\ Server
    
    startupitem.create      yes
    startupitem.executable  ${prefix}/lib/${name_mysql}/bin/mysqld --user=${mysqluser}
    
    use_configure       no
    
    build {}
    
    destroot {
        xinstall -m 755 -o ${mysqluser} -g ${mysqluser} -d \
            ${destroot}${prefix}/etc/${name_mysql} \
            ${destroot}${prefix}/var/db/${name_mysql} \
            ${destroot}${prefix}/var/log/${name_mysql} \
            ${destroot}${prefix}/var/run/${name_mysql}
        destroot.keepdirs-append  \
            ${destroot}${prefix}/var/db/${name_mysql} \
            ${destroot}${prefix}/var/log/${name_mysql} \
            ${destroot}${prefix}/var/run/${name_mysql}
    }

    notes "
If this is a new install you might want to run:

\$ sudo -u ${mysqluser} ${prefix}/lib/${name_mysql}/bin/mysql_install_db
"
    
    livecheck.type          none
}
