# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem      1.0
PortGroup       github 1.0

github.setup    tenomoto dotwrp 1.1 v
revision        3
categories      math science
platforms       darwin
maintainers     takeshi
license         public-domain
description     A wrapper for *dot* functions in Accelerate framework
long_description \
    This is a wrapper for four Level 1 BLAS functions \
    CDOTC CDOTU ZDOTC ZDOTU and SDOT in Accelerate.framework \
    of Mac OS X.

checksums           rmd160  c9aa3d56d6d01c7b198a100544e6a84748022405 \
                    sha256  7ffa40d6a3564829a85757af200c9fd71482cf729a266a23b4775997450d7397

use_configure   no

configure.fflags-append -fno-underscoring
build {
    system "cd ${worksrcpath};
        ${configure.fc} ${configure.fflags} -c ${name}.f90;
        /usr/bin/ar -cru lib${name}.a ${name}.o;
        /usr/bin/ranlib lib${name}.a"
}

destroot {
    xinstall -m 644 ${worksrcpath}/lib${name}.a ${destroot}${prefix}/lib
}

set gcc_versions {4.3 4.4 4.5 4.6 4.7 4.8 4.9}
set default_fortran_variant +gcc48
set g95_conflicts {}

foreach ver ${gcc_versions} {
    set ver_no_dot [join [split ${ver} "."] ""]

    set variant_line {variant gcc${ver_no_dot} description "build with gfortran from gcc${ver_no_dot}" conflicts g95}

    foreach over ${gcc_versions} {
        if {${ver} == ${over}} {
            continue
        }

        set over_no_dot [join [split ${over} "."] ""]
        append variant_line " conflicts gcc${over_no_dot}"
    }
    append variant_line { {}}

    eval $variant_line

    append g95_conflicts " conflicts gcc${ver_no_dot}"

    if {[variant_isset gcc${ver_no_dot}]} {
        if {${default_fortran_variant} != "+gcc${ver_no_dot}"} {
            set default_fortran_variant ""
        }
    }
}

eval [concat {variant g95 description {build with g95}} $g95_conflicts {{}}]

if {[variant_isset g95]} {
    if {${default_fortran_variant} != "+g95"} {
        set default_fortran_variant ""
    }
}

if {${default_fortran_variant} != ""} {
    default_variants-append "${default_fortran_variant}"
}

foreach ver ${gcc_versions} {
    set ver_no_dot [join [split ${ver} "."] ""]

    if {[variant_isset gcc${ver_no_dot}]} {
        depends_lib-append path:lib/libgcc/libgcc_s.1.dylib:libgcc
        depends_build-append port:gcc${ver_no_dot}

        configure.fc  ${prefix}/bin/gfortran-mp-${ver}
        configure.f77 ${prefix}/bin/gfortran-mp-${ver}
        configure.f90 ${prefix}/bin/gfortran-mp-${ver}
    }
}

if {[variant_isset g95]} {
    depends_lib-append path:lib/libgcc/libgcc_s.1.dylib:libgcc
    depends_build-append port:g95

    configure.fc ${prefix}/bin/g95
    configure.f77 ${prefix}/bin/g95
    configure.f90 ${prefix}/bin/g95
}
