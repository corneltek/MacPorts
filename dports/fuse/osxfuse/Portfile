# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0

name                osxfuse
version             2.7.0
categories          fuse devel
platforms           macosx
license             BSD APSL
maintainers         dports openmaintainer

description         A FUSE-Compliant File System Implementation Mechanism \
                    for Mac OS X
long_description    FUSE for OS X implements a mechanism that makes it possible to implement \
                    a fully functional file system in a user-space program on Mac OS X. It \
                    aims to be API-compliant with the FUSE (File-system in USErspace) \
                    mechanism that originated on Linux.  Therefore, many existing FUSE file \
                    systems become readily usable on Mac OS X. This port provides the \
                    user-space library and header files for building filesystems.

homepage            http://osxfuse.github.io/

livecheck.type      regex
livecheck.url       ${homepage}
livecheck.regex     "[quotemeta [string toupper ${name}]] (\\d+(?:\\.\\d+)*)"

# We will build user-space components for the specified arch, and
# kernel modules for the kernel arch.
set kernel_arch [exec uname -m]
# If building for different kernel arch than our buildslaves use, force a build
# from source. The binary package IDs don't encode the kernel arch.
if {${kernel_arch} ne "x86_64"} {
    archive_sites
}

distfiles
set mp.dist {
    osxfuse     8eb90a2   {rmd160 563f74f3e17caa644be3f01290f4587aea164a56 sha256 82ad7fe7c2b1c31ef522186f0761fb3ab2817027e5dd5cbe85be690f70e846b1}
    kext        122937c   {rmd160 b912555b99c832a1a6d2cad9d0b6c667e906c8b5 sha256 723b89ac3c52eea2ed3eeb02fc838886e6d4bce29c605761413d89085a03778e}
    framework   9fdde8a   {rmd160 4124b3e9c396ee41dfce35d480caff41936b8000 sha256 9dd705e05de0b346000853c08c143a15d73673684afcc9bbcf55e6c71b33fb87}
    prefpane    99d92a5   {rmd160 04706b6e84b8a2ac2a430b53c66c38f34af92b3f sha256 9d8d2990c0f72e1953f1506f9d84bc18299f4b8b9f691c3ecdc4a98b5a81d20b}
    fuse        98b9960   {rmd160 589cbe0c819caded56dc4be64727580e1400fd9e sha256 f60205e19c2971228b78d180dc65cda2a6271a567229acdeb19567fcbcebb641}
    support     4b8cfd3   {rmd160 1c03c561c6c8a0650421393857fb356330ea1926 sha256 b30899548a573968b62c109d3394f12f8ef9f79ab981f8e559ba7ab7be77e58a}
}

depends_build       port:autoconf \
                    port:automake \
                    port:libtool

set mp.osxfuse_rev  [lindex ${mp.dist} [expr [lsearch ${mp.dist} osxfuse] + 1]]
worksrcdir          osxfuse-osxfuse-${mp.osxfuse_rev}

foreach { mp.comp mp.rev mp.chksum } ${mp.dist} {
    set f ${mp.comp}-${mp.rev}.tar.gz
    master_sites-append https://github.com/osxfuse/${mp.comp}/tarball/${mp.rev}/:${mp.comp}
    distfiles-append    ${f}:${mp.comp}
    checksums-append    ${f}
    foreach {type chksum} ${mp.chksum} {
        checksums-append ${type} ${chksum}
    }
}

post-extract {
    foreach { mp.comp mp.rev mp.chksum } ${mp.dist} {
        if {${mp.comp} ne "osxfuse"} {
            # Replace existing empty directory if it exists
            file delete ${workpath}/${worksrcdir}/${mp.comp}
            move ${workpath}/osxfuse-${mp.comp}-${mp.rev} ${workpath}/${worksrcdir}/${mp.comp}
        }
    }
}

patchfiles          patch-buildsystem.diff \
                    patch-kext-location.diff

post-patch {
    # Perhaps the ports sandbox does not let us write to /tmp.
    # For a quick and temporary fix use this reinplace.
    file mkdir ${workpath}/tmp
    reinplace "s,@@TMP@@,${workpath}/tmp,g" ${worksrcpath}/build.sh

    # Only build the archs we want, not the hardcoded universal archs
    reinplace -E "s,@@ARCHS@@,[get_canonical_archs],g" ${worksrcpath}/build.sh
    reinplace -E "s,@@KERNELARCHS@@,${kernel_arch},g" ${worksrcpath}/build.sh

    # Inject the destroot path as the buildsystem tries to write to the prefix directly
    reinplace -E "s,@@DESTROOT@@,${destroot},g" ${worksrcpath}/build.sh

    # Correct the location of the kext
    reinplace -E "s,@@PREFIX@@,${prefix},g" ${worksrcpath}/kext/common/fuse_param.h
}

use_configure   no

build.cmd       ./build.sh
# Yes, they really named this target homebrew
build.target    -t homebrew
build.args      -f ${prefix}

# The build step already does everything
destroot {}

# Move filesystem bundle into place
post-destroot {
    # Set proper permissions
    fs-traverse f ${destroot}${prefix}/Library {
        file attributes $f -owner root -group wheel
    }
    # Enable setuid on helper binary
    file attributes ${destroot}${prefix}/Library/Filesystems/osxfusefs.fs/Support/load_osxfusefs -permissions 04755
}
destroot.violate_mtree yes

notes {
    When upgrading, unmount all FUSE filesystems and then unload the kernel extension.
    Unloading can be done via: sudo kextunload -b com.github.osxfuse.filesystems.osxfusefs
    Alternativley (or if this fails), just reboot your computer now.
}

# Could probably be supported by setting ARCHS correctly above
universal_variant no
