# -*- coding: utf-8; mode: tcl; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:noet:sw=4:ts=8:sts=4
# $Id$

PortSystem  1.0

name        p5-graveyard
version     0.0
categories  perl

array set replacements {
    5.16 5.22
    5.18 5.22
    5.20 5.22
}
set defaultBranches {5.16 5.18 5.20}

proc unknown args {
    upvar 1 replacements replacements defaultBranches defaultBranches
    if {[llength $args] < 3} {
        lappend args {*}$defaultBranches
    }
    set replaced_branches [lassign $args superport version_string]
    lassign [split $version_string _] ver rev
    if {$rev eq ""} {
        set rev 0
    }
    foreach branch $replaced_branches {
        regsub ^p5- $superport p${branch}- replaced
        regsub ^p5- $superport p$replacements($branch)- replacement
        subport $replaced {
            version $ver
            revision $rev
            replaced_by $replacement
        }
    }
}

# NOTE: To keep file size reasonable, this list uses tabs for formatting
# instead of spaces. When inserting new entries, please set your
# editor's tab width to 8 to ensure proper alignment, and verify that
# you're inserting tab characters and not spaces.
#
# The current list was generated at YYYY-mm-ddTHH:MM:SSZ with:
#
# $ port -q info --index --line --version --revision --name 'p5-*' | awk '
#       BEGIN {
#           blacklist["p5-eperl"] = ""
#           blacklist["p5-graveyard"] = ""
#           blacklist["p5-sdl_perl"] = ""
#       }
#       {
#           if ($3 in blacklist)
#               next
#           padding = (48 - length($3)) / 8
#           if (padding != int(padding))
#               padding = int(padding) + 1
#           printf "%s", $3
#           for (i = 0; i < padding; ++i)
#               printf "\t"
#           printf "%s_%d%s", $1, ($2 + 1), ORS
#       }
#   '

#                                                               REPLACED BRANCHES
#                                                               (optional, defaults to
# SUPERPORT                                     VERSION+REV     "5.16 5.18 5.20")


# MAUSOLEUM (separate section for special ports)
foreach branch $defaultBranches {
    subport subversion-perlbindings-$branch {
        version 1.9.3
        revision 1
        replaced_by subversion-perlbindings-$replacements($branch)
    }
}


if {${subport} ne ${name}} {
    PortGroup obsolete 1.0
} else {
    platforms           darwin freebsd linux
    supported_archs     noarch
    universal_variant   no
    maintainers         larryv openmaintainer
    description         Metaport containing obsolete Perl subports
    long_description    This is a metaport for collecting obsolete \
                        Perl subports so they don't cause spurious \
                        build failures on the MacPorts buildslaves \
                        whenever their superports are modified. It \
                        cannot be installed.
    homepage            https://www.macports.org
    archive_sites
    master_sites
    pre-fetch {
        ui_error {This is a metaport for obsolete Perl subports.}
        return -code error {non-installable metaport}
    }
}
