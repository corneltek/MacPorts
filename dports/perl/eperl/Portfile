# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem              1.0

name                    eperl
version                 2.2.14

maintainers             cal openmaintainer
categories              perl www
platforms               darwin

homepage                http://www.ossp.org/pkg/tool/eperl/
license                 {Artistic-1 GPL}
description             Embedded Perl Language
long_description        ePerl interprets an ASCII file bristled with Perl 5 \
                        program statements by evaluating the Perl 5 code while \
                        passing through the plain ASCII data.

master_sites            http://distcache.freebsd.org/ports-distfiles/

checksums               rmd160  708c72d4a2bc494e9b3e953aa13efd0fe7933b98 \
                        sha256  a9add61ed59e3030027e0ff5c00195627d7309be48162c9b315f57d27730a295

use_configure           yes

## Most patches are copied from the Debian distribution, which uses patch
## files with a prefix
patch.pre_args          -p1
patchfiles              0001-copyright-updates.diff \
                        0002-eperl_2.2.14-15.2.diff \
                        0003-fix-spelling-errors.diff \
                        0004-document-double-shebang.diff \
                        0005-fix-preprocessor-comments-to-behave-as-documented.diff \
                        0006-fix-format-string-error-in-perl-stderr.diff \
                        0007-debian-2.2.14-16-modified-and-added-shtool.diff \
                        0008-wml-2.0.11-merge.diff \
                        0009-configure-use-correct-perl.diff \
                        0010-redhat-with-thr.diff

post-patch {
    # this file is added by 0007-*.diff, but must be executable
    file attributes "${worksrcpath}/etc/shtool" -permissions +x
}

configure.args          --enable-debug \
                        CC=\"${configure.cc} [get_canonical_archflags cc]\" \
                        LD=\"${configure.cc} [get_canonical_archflags ld]\"

destroot.destdir        prefix=${destroot}${prefix} \
                        mandir=${destroot}${prefix}/share/man/man1

test.run                yes

# Choose the perl versions to support
set perlversions        {5.16 5.18 5.20}
# Set the version that should be used as default
set defaultversion      5.16

# Internal helper variables
set default_version     [string map {. _} $defaultversion]
set nondefault_set      no

# Generate the perl variants
foreach perlversion $perlversions {
    set variantname "perl[string map {. _} $perlversion]"

    # so, if Tcl did support mapâ€¦ http://wiki.tcl.tk/12848
    set conflicts [list]
    foreach otherversion [lsearch -all -inline -not -exact -sorted $perlversions $perlversion] {
        lappend conflicts "perl[string map {. _} $otherversion]"
    }

    variant $variantname description "Use perl ${perlversion}" conflicts {*}$conflicts [subst {
        depends_lib-append      port:perl${perlversion}

        configure.args-append   ${prefix}/bin/perl${perlversion}
    }]

    if {[variant_isset $variantname] && $perlversion ne $defaultversion} {
        set nondefault_set yes
    }
}

if {![tbool nondefault_set]} {
    default_variants +perl${default_version}
    if {![variant_isset perl${default_version}]} {
        ui_error "${name}: You must choose at least one perl variant"
        error "Error evaluating variants"
    }
}

livecheck.type          regex
livecheck.url           ${homepage}
livecheck.regex         {class="heavy">(\d+(?:\.\d+)*)<}
