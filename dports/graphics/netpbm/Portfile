# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$

PortSystem          1.0
PortGroup           conflicts_build 1.0

name                netpbm
version             10.70.02
svn.revision        2463
categories          graphics
platforms           darwin freebsd linux
maintainers         ryandesign openmaintainer
license             {BSD GPL-2 IJG Permissive}

description         Image manipulation

long_description    A whole bunch of utilities for primitive manipulation \
                    of graphic images. Wide array of converters from one \
                    graphics format to another, e.g. from g3 fax format to \
                    jpeg. Many basic graphics editing tools such as \
                    magnifying and cropping.

homepage            http://netpbm.sourceforge.net/

depends_build       path:bin/perl:perl5

depends_lib         port:jasper \
                    port:jbigkit \
                    port:jpeg \
                    port:libpng \
                    port:libxml2 \
                    port:tiff \
                    port:zlib

fetch.type          svn
svn.url             https://svn.code.sf.net/p/netpbm/code/advanced/
worksrcdir          advanced

patchfiles          patch-converter-other-giftopnm.c-strcaseeq.diff \
                    patch-lib-Makefile.diff \
                    patch-other-pamx-Makefile.diff \
                    patch-test-Execute-Tests.diff \
                    patch-test-ppmdim.test.diff

conflicts_build     ${name}

variant universal {}

configure.cflags-append -fno-common

set jasperlib -ljasper
set ldreloc NONE
set ldshlib {-shared -Wl,-soname,$(SONAME)}
set linker_can_do_explicit_library N
set netpbmlibtype unixshared
set netpbmlibsuffix so
set want_sse N
set want_x11 N

configure {
    # The netpbm configure script is interactive, and appends its results to
    # the config.mk file; doc/INSTALL recommends packagers do that manually.
    copy -force ${worksrcpath}/config.mk.in ${worksrcpath}/config.mk
    set configmk [open ${worksrcpath}/config.mk a]
    puts ${configmk} "
CC = ${configure.cc}
CFLAGS = ${configure.cflags} ${configure.cppflags} [get_canonical_archflags cc]
CFLAGS_CONFIG = \$(CFLAGS)
JASPERHDR_DIR = ${prefix}/include
JASPERLIB = ${jasperlib}
JBIGHDR_DIR = ${prefix}/include
JBIGLIB = -ljbig
JPEGLIB = -ljpeg
LDFLAGS = ${configure.ldflags} [get_canonical_archflags ld]
LDRELOC = ${ldreloc}
LDSHLIB = ${ldshlib}
LINKER_CAN_DO_EXPLICIT_LIBRARY = ${linker_can_do_explicit_library}
NETPBMLIBTYPE = ${netpbmlibtype}
NETPBMLIBSUFFIX = ${netpbmlibsuffix}
PNGLIB = -lpng
RGB_DB_PATH = ${prefix}/share/netpbm/rgb.txt
TIFFLIB = -ltiff
WANT_SSE = ${want_sse}
WANT_X11 = ${want_x11}
ZLIB = -lz
"
    close ${configmk}
}

platform darwin {
    set ldshlib "\$(LDFLAGS) -dynamiclib -install_name ${prefix}/lib/libnetpbm.\$(MAJ).dylib -compatibility_version \$(MAJ) -current_version \$(MAJ).\$(MIN).\$(NETPBM_POINT_RELEASE)"
    set netpbmlibtype dylib
    set netpbmlibsuffix dylib
}

platform linux {
    set ldreloc {ld --reloc}
    set linker_can_do_explicit_library Y
}

platform freebsd {
    depends_build-append port:libtool

    patchfiles-append patch-libopt.c.diff

    set jasperlib ${prefix}/lib/libjasper.la
    set ldshlib "${prefix}/lib/glibtool --mode=link gcc"
}

build.args          messages=yes
build.target
build.type          gnu

set pkgdir          ${workpath}/package

post-build {
    system -W ${worksrcpath} "${build.cmd} package pkgdir=${pkgdir}"
    delete ${pkgdir}/bin/doc.url
    # Problem setting PKGMANDIR in config.mk reported to developer; might be fixed after 10.70.01
    file mkdir ${pkgdir}/share/
    move ${pkgdir}/man ${pkgdir}/share/
}

test.run            yes
test.target         check
test.args           pkgdir=${pkgdir} \
                    resultdir=${workpath}/test \
                    tmpdir=${workpath}/.tmp
# Problem reported to developer; might be fixed after 10.70.01
test.env            DYLD_LIBRARY_PATH=${pkgdir}/lib

destroot {
    delete ${destroot}${prefix}/bin ${destroot}${prefix}/share
    copy ${pkgdir}/bin ${pkgdir}/share ${destroot}${prefix}/
    copy ${pkgdir}/include/${name} ${destroot}${prefix}/include/
    copy {*}[glob ${pkgdir}/lib/*] {*}[glob ${pkgdir}/link/*] ${destroot}${prefix}/lib/

    xinstall -d ${destroot}${prefix}/share/${name}/
    copy {*}[glob ${pkgdir}/misc/*] ${destroot}${prefix}/share/${name}/

    reinplace -W ${pkgdir} "/^@/d" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@BINDIR@|${prefix}/bin|g" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@DATADIR@|${prefix}/share/${name}|g" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@INCLUDEDIR@|${prefix}/include|g" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@LIBDIR@|${prefix}/lib|g" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@LINKDIR@|${prefix}/lib|g" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@MANDIR@|${prefix}/share/man|g" config_template pkgconfig_template
    reinplace -W ${pkgdir} "s|@VERSION@|${version}|g" config_template pkgconfig_template
    xinstall -m 755 ${pkgdir}/config_template ${destroot}${prefix}/bin/netpbm-config
    xinstall -m 644 ${pkgdir}/pkgconfig_template ${destroot}${prefix}/lib/pkgconfig/netpbm.pc

    foreach old_library {pbm pgm pnm ppm} {
        ln -s libnetpbm.dylib ${destroot}${prefix}/lib/lib${old_library}.dylib
    }

    set docdir ${prefix}/share/doc/${subport}
    xinstall -d ${destroot}${docdir}
    xinstall -m 644 -W ${worksrcpath} \
        README \
        doc/COPYRIGHT.PATENT \
        doc/GPL_LICENSE.txt \
        doc/HISTORY \
        doc/Netpbm.programming \
        doc/USERDOC \
        doc/copyright_summary \
        doc/lgpl_v21.txt \
        doc/patent_summary \
        ${destroot}${docdir}
}

# work around bug in Apple's gcc build 4061
platform darwin 8 {
    depends_build-append port:gmake
    build.cmd ${prefix}/bin/gmake
    pre-configure {
        set fl [open "| ${configure.cc} --version"]
        set data [read $fl]
        close $fl
        if {[regexp "build 4061" ${data}]} {
            ui_msg "On Mac OS X ${macosx_version}, ${name} ${version} does not work with gcc version \"${data}\"."
            return -code error "incompatible gcc version"
        }
    }
}

variant x11 description {Build the pamx image viewer} {
    global want_x11
    set want_x11        Y
    depends_build-append port:pkgconfig
    depends_lib-append  port:xorg-libX11
}

default_variants    +x11

livecheck.type      regex
livecheck.url       https://sourceforge.net/p/netpbm/code/HEAD/log/?path=/advanced/version.mk&limit=1
livecheck.regex     {(\d+(\.\d+)+)}
